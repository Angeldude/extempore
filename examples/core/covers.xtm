;;;
;;
;; A few fun covers to demo/test the "analogue" synth
;;

;; Author: Andrew Sorensen
;; Keywords: extempore

;;; Commentary:

;;; Code:

(sys:load "libs/external/instruments_ext.xtm")

;; make bass, lead, riff and pad
(make-instrument bass analogue)
(make-instrument lead analogue)
(make-instrument riff analogue)
(make-instrument pad analogue)

(define R repeat)

(bind-func dsp:DSP
  (lambda (in time chan dat)
    (cond ((< chan 2)
           (+ (* 1.0 (lead in time chan dat))
              (* 1.0 (pad in time chan dat))
              (* 1.0 (riff in time chan dat))
              (* 1.0 (bass in time chan dat))))
          (else 0.0))))

(dsp:set! dsp)

;;
;; analogue synth patches
;;

;; whiter shade of pale

(bind-func analogue_pale_organ
  (lambda (inst)
    (analogue_reset inst)
    
    ;; oscs
    (set_osc1_amp inst 0.15)
    (set_osc1_wave inst PARAM_SINE)
    (set_osc1_tune inst 1.0)
    (set_osc1_pan inst 0.5)

    (set_osc4_amp inst 0.15)
    (set_osc4_wave inst PARAM_SINE)
    (set_osc4_tune inst 0.996)
    (set_osc4_pan inst 0.5)
    
    (set_osc2_amp inst 0.15)
    (set_osc2_wave inst PARAM_SINE)
    (set_osc2_tune inst 1.501)
    (set_osc2_pan inst 0.35)    
    
    (set_osc3_amp inst 0.15)
    (set_osc3_wave inst PARAM_SINE)
    (set_osc3_tune inst 2.01)
    (set_osc3_pan inst 0.15)

    (set_noise_type inst PARAM_PINK)
    (set_noise_amp inst 0.01)

    ;; poor mans leslie speaker :)
    (set_flanger_rate inst 0.005315)
    (set_flanger_low inst 1.0)
    (set_flanger_high inst 3.0)
    (set_flanger_fb inst 0.3)
    (set_flanger_mix inst 0.3)
    
    ;; amplitude
    (set_amp_env inst 0.0 0.0 1.0 1000.0)        
    (set_gain inst 10.0)

    ;; polyphonic
    (set_polyphonic inst)    
    void))


;; oxygene

(bind-func analogue_oxygene_pad
  (lambda (inst)
    (analogue_reset inst)
    ;; oscs
    (set_osc1_amp inst 0.25)
    (set_osc1_wave inst PARAM_SAW)
    (set_osc1_tune inst 0.5)
    (set_osc1_pan inst 0.5)

    (set_osc2_amp inst 0.25)
    (set_osc2_wave inst PARAM_SAW)
    (set_osc2_tune inst 0.5011)
    (set_osc2_pan inst 0.35)    
    
    (set_osc3_amp inst 0.25)
    (set_osc3_wave inst PARAM_SAW)
    (set_osc3_tune inst 0.4997)
    (set_osc3_pan inst 0.65)        

    (set_noise_type inst PARAM_WHITE)
    (set_noise_amp inst 0.7)
    
    ;; filter
    (set_filter_frq inst 1000.0)
    (set_filter_res inst 0.7)
    (set_filter_type inst PARAM_BPF)
    (set_mod_matrix inst PARAM_LFO1 PARAM_FILTER_FRQ 1.0)
    (set_lfo inst 1 950.0 0.013812)    

    ;; poor mans phasor :(
    (set_flanger_rate inst 0.07315)
    (set_flanger_low inst 0.005)
    (set_flanger_high inst 13.0)
    (set_flanger_fb inst 0.6)
    (set_flanger_mix inst 0.35)

    ;; amplitude
    (set_amp_env inst 500.0 0.0 1.0 3000.0)        
    (set_gain inst 10.0)

    ;; polyphonic
    (set_polyphonic inst)
    void))

(bind-func analogue_oxygene_riff
  (lambda (inst)
    (analogue_reset inst)
    ;; oscs
    (set_osc1_amp inst 0.4)
    (set_osc1_wave inst PARAM_SAW)
    (set_osc1_tune inst 0.5)

    ;; filter
    (set_filter_frq inst 500.0)
    (set_filter_env inst 0.0 400.0 0.0 100.0)
    (set_filter_env_amt inst 300.0)
    (set_filter_res inst 0.6)

    ;; amplitude
    (set_amp_env inst 10.0 400.0 0.0 100.0)        
    (set_gain inst 20.0)

    ;; polyphonic
    (set_monophonic inst)
    void))


(bind-func analogue_oxygene_bass
  (lambda (inst)
    (analogue_reset inst)
    ;; oscs
    (set_osc1_amp inst 0.4)
    (set_osc1_wave inst PARAM_PULSE)
    (set_osc1_tune inst 0.25)
    (set_osc1_pw inst 0.5) ;; square

    (set_osc2_amp inst 0.4)
    (set_osc2_wave inst PARAM_SAW)
    (set_osc2_tune inst 0.5)
    
    ;; filter
    (set_filter_frq inst 400.0)
    (set_filter_res inst 0.0)
    (set_filter_env inst 0.0 100.0 0.0 750.0)
    (set_filter_env_amt inst 300.0)
    
    ;; amplitude
    (set_amp_env inst 0.0 0.0 1.0 750.0)        
    (set_gain inst 10.0)

    ;; polyphony
    (set_monophonic inst)
    (set_legato inst 1)   
    void))


(bind-func analogue_oxygene_lead
  (lambda (inst)
    (analogue_reset inst)
    ;; oscs
    (set_osc1_amp inst 0.4)
    (set_osc1_wave inst PARAM_SAW)
    (set_osc1_tune inst 0.5)

    (set_osc2_amp inst 0.4)
    (set_osc2_wave inst PARAM_SAW)
    (set_osc2_tune inst 0.50025)
    
    (set_osc3_amp inst 0.4)
    (set_osc3_wave inst PARAM_SAW)
    (set_osc3_tune inst 0.4996)
    
    ;; filter
    (set_filter_frq inst 2000.0)
    (set_filter_env_follow_frq inst 1.5)
    (set_filter_env inst 0.0 600.0 0.0 0.0)            
    (set_filter_env_amt inst -1500.0)
    (set_filter_res inst 0.0)

    ;; set delay
    (set_delay_mix inst 0.3)
    (set_delay_time_left inst (* 0.125 .125 (/ 60.0 125.0) SRs))
    (set_delay_time_right inst (* 0.125 .125 (/ 60.0 125.0) SRs))
    (set_delay_fb inst 0.6)

    ;; amplitude
    (set_amp_env inst 125.0 0.0 1.0 600.0)        
    (set_gain inst 15.0)

    ;; polyphonic
    (set_polyphonic inst)
    void))

(analogue_oxygene_lead lead)

;; dr who

(bind-func analogue_drwho_bass
  (lambda (inst)
    (analogue_reset inst)
    ;; oscs
    (set_osc1_amp inst 0.4)
    (set_osc1_wave inst PARAM_PULSE)
    (set_osc1_tune inst 0.5)
    (set_osc1_pan inst 0.4)
    (set_osc1_pw inst 0.55)

    (set_osc2_amp inst 0.4)
    (set_osc2_wave inst PARAM_PULSE)
    (set_osc2_tune inst 1.001)
    (set_osc2_pan inst 0.5)
    (set_osc2_pw inst 0.65)
    
    (set_osc3_amp inst 0.2)
    (set_osc3_wave inst PARAM_PULSE)
    (set_osc3_tune inst 2.0025)
    (set_osc3_pan inst 0.6)
    (set_osc3_pw inst 0.75)

    (set_osc4_amp inst 0.5)
    (set_osc4_wave inst PARAM_SINE)
    (set_osc4_tune inst 0.2501)
    (set_osc4_pan inst 0.5)
    (set_osc4_pw inst 0.5)
    
    ;; amp env
    (set_amp_env inst 5.0 1000.0 0.1 250.0)    

    ;; filter
    (set_filter_frq inst 700.0) ;; 700 hz
    (set_filter_env inst 5.0 200.0 0.1 250.0)
    (set_filter_env_amt inst 2500.0)
    (set_filter_res inst 0.25)

    ;; pitch env
    (set_pitch_env inst 100.0 0.0)
    (set_pitch_env_amt inst -0.025)

    ;; set reverb
    (set_reverb_mix inst 0.5)
    (set_reverb_size inst 0.8)

    ;; set gain
    (set_gain inst 5.0)

    ;; polyphonic
    (set_polyphonic inst)
    void))

(bind-func analogue_drwho_lead
  (lambda (inst)
    (analogue_reset inst)
    ;; oscs
    (set_osc1_amp inst 0.6)
    (set_osc1_wave inst PARAM_SINE)
    (set_osc1_tune inst 1.0)

    (set_osc3_amp inst 0.6)
    (set_osc3_wave inst PARAM_SINE)
    (set_osc3_tune inst 1.00728)
    
    (set_osc2_amp inst 0.3)
    (set_osc2_wave inst PARAM_PULSE)
    (set_osc2_tune inst 2.003)
    (set_osc2_pw inst 0.5) ;; sqr
    
    ;; frq mod osc1
    (set_lfo inst PARAM_LFO1 1.0 3.128)    
    (set_mod_matrix inst PARAM_LFO1 PARAM_OSC1_FRQ 9.3738)
    
    ;; amp env
    (set_amp_env inst 50.0 100.0 0.8 3000.0)
    
    ;; filter
    (set_filter_frq inst 750.0) 
    (set_filter_env_amt inst 0.0) ;; no envelope
    (set_filter_res inst 0.8)
    (set_filter_env_follow_frq inst 2.0)
    
    ;; set reverb
    (set_reverb_mix inst 0.65)
    (set_reverb_size inst 0.8)
    
    ;; mono synth
    (set_monophonic inst)
    (set_portamento inst 80.0)
    (set_legato inst 1)
    
    ;; set gain
    (set_gain inst 6.0)
    void))


;;;;;;;;;;; now the music


;;
;; white shade of pale
;;

;; organ sound for lead and bassline
(analogue_pale_organ pad)

;; tempo 
(*metro* 'set-tempo 88.0)

(define whiter-shade
  (lambda (beat dur)
    (playp 32 0 pad 12 `(e4 ,@(R __ 7) __ d4 c4 b3 c4 d4 e4 c4
                          a4 ,@(R __ 5) b4 c5 (f4 e4) f4 ,@(R __ 4) d4 __
                          b4 ,@(R __ 5) c5 d5 (g4 f4) g4 ,@(R __ 3) f4 e4 f4
                          f4 e4 d4 c4 a3 b3 c4 d4 b3 c4 d4 e4 f4 e4 f4 d4)
           80
           `(9/2 ,@(R 0 7) 0 ,@(R 1/2 7)
                 3 ,@(R 0 5) 1/2 1/2 (1/4 1/4) 2 ,@(R 0 4) 1 0
                 3 ,@(R 0 5) 1/2 1/2 (1/4 1/4) 2 ,@(R 0 3) 1/2 1/2 1/2
                 ,@(R 1/2 16)))
    (playp 32 0 pad 0 `(c4 b3 a3 g3 f4 e4 d4 c4 g4 f4 e4 d4 c4 f4 g4 b3)
           80 2)
    (callback (*metro* (+ beat (* .5 dur))) 'whiter-shade (+ beat dur) dur)))

(whiter-shade (*metro* 'get-beat 4) 1/4)


;;
;; oxygene
;;

(analogue_oxygene_bass bass)
(analogue_oxygene_riff riff)
(analogue_oxygene_pad pad)
(analogue_oxygene_lead lead)

;; tempo
(*metro* 'set-tempo 125.0)

(define oxygene
  (lambda (beat dur)
    (onbeat? 1 32
             (map (lambda (p)
                    (play pad p 90 16))
                  (pc:make-chord 50 63 3 '(0 3 7))))
    (onbeat? 16 32
             (map (lambda (p)
                    (play pad p 90 8))
                  (pc:make-chord 50 63 3 '(2 5 9 0))))
    (onbeat? 24 32
             (map (lambda (p)
                    (play pad p 90 8))
                  (pc:make-chord 50 63 3 '(2 5 9 0))))
    (playp 32 0 riff 24 `(,@(R `(48 48 48) 16)
                          ,@(R `(50 50 50) 8)
                          ,@(R `(48 48 48) 8))
           (R `(90 70 70) 32) 1/4)
    (playp 32 0 lead 12 `(,@(R `(72 (__ __ 67) (63 __ 67) (__ __ 60) ,@(R __ 4)) 2)
                          ,@(R `(70 (__ __ 69) (67 __ 69) (__ __ 62) ,@(R __ 4)) 1)
                          ,(R `((69 __ 67) (65 __ 60) __ __) 2))
           (cosr 80 20 2) (* 2.0 dur))
    (playp 32 0 bass `(,@(R `((60 __ 60) (58 __ 60) (__ __ 55) (58 __ 55)) 4)
                       ,@(R `((62 __ 62) (60 __ 62) (__ __ 57) (60 __ 57)) 2)
                       ,@(R `((65 __ 65) (63 __ 60) (__ __ 65) (63 __ 60)) 2))
           (cosr 80 20 2) 1/4)
    (callback (*metro* (+ beat (* .5 dur))) 'oxygene (+ beat dur) dur)))

(oxygene (*metro* 'get-beat 4) 1/4)



;;
;; dr who
;; 

(analogue_drwho_bass bass)
(analogue_drwho_lead lead)
;; resuse oxygene pad :)
(analogue_oxygene_pad pad)

(*metro* 'set-tempo 125.0)

(define drwho
  (lambda (beat dur)
    (onbeat? 1 16
             (map (lambda (p)
                    (play pad p 60 16))
                  (pc:make-chord 40 80 4 '(4 7 2))))
    (playp 8 0 bass 0 `(,@(R `(e3 __ e3) 6) (g3 __ g3) (g3 __ d3)) (cosr 85 20 2) 0.5)
    (playp 32 0 lead 0 `((b4 b4 b4 d5)
                           (b4 b4 b4 __)
                           (b4 g4 g4 b3 d4 (d4 __ c4) b3 (b3 __ a3))
                           (b3 b3 b3 (b3 c4 d4) b3 __ b3 c5))
           (cosr 100 10 3) 2.5)
    (callback (*metro* (+ beat (* .5 dur))) 'drwho (+ beat dur) dur)))

(drwho (*metro* 'get-beat 4) 1/4)

