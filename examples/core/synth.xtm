;;; synth.xtm -- a simple little synth example

;; Author: Andrew Sorensen
;; Keywords: extempore

;;; Commentary:

;;; Code:

(sys:load "libs/core/instruments.xtm")

(make-instrument bass analog)
(make-instrument lead analog)

(bind-func dsp:DSP
  (lambda (in time chan dat)
    (cond ((< chan 2)
           (+ (* 1.0 (lead in time chan dat))
              (* 1.0 (bass in time chan dat))))
          (else 0.0))))

(dsp:set! dsp)

(bind-func drwho_bass
  (lambda (inst)
    (synth_reset inst)
    ;; oscs
    (set_osc1_amp inst 0.4)
    (set_osc1_wave inst PARAM_PULSE)
    (set_osc1_tune inst 0.5)
    (set_osc1_pan inst 0.4)
    (set_osc1_pw inst 0.55)

    (set_osc2_amp inst 0.4)
    (set_osc2_wave inst PARAM_PULSE)
    (set_osc2_tune inst 1.001)
    (set_osc2_pan inst 0.5)
    (set_osc2_pw inst 0.65)
    
    (set_osc3_amp inst 0.2)
    (set_osc3_wave inst PARAM_PULSE)
    (set_osc3_tune inst 2.0025)
    (set_osc3_pan inst 0.6)
    (set_osc3_pw inst 0.75)

    (set_osc4_amp inst 0.5)
    (set_osc4_wave inst PARAM_SINE)
    (set_osc4_tune inst 0.2501)
    (set_osc4_pan inst 0.5)
    (set_osc4_pw inst 0.5)
    
    ;; amp env
    (set_amp_env inst 5.0 1000.0 0.1 250.0)    

    ;; filter
    (set_filter_frq inst 700.0) ;; 700 hz
    (set_filter_env inst 5.0 200.0 0.1 250.0)
    (set_filter_env_amt inst 2500.0)
    (set_filter_res inst 0.25)

    ;; pitch env
    (set_pitch_env inst 100.0 0.0)
    (set_pitch_env_amt inst -0.01)

    ;; set reverb
    (set_reverb_mix inst 0.5)
    (set_reverb_size inst 0.8)

    ;; set gain
    (set_gain inst 5.0)

    ;; polyphonic
    (set_polyphonic inst)
    void))


(bind-func drwho_lead
  (lambda (inst)
    (synth_reset inst)
    ;; oscs
    (set_osc1_amp inst 0.6)
    (set_osc1_wave inst PARAM_SINE)
    (set_osc1_tune inst 1.0)

    (set_osc2_amp inst 0.4)
    (set_osc2_wave inst PARAM_PULSE)
    (set_osc2_tune inst 2.003)
    (set_osc2_pw inst 0.5) ;; sqr
    
    ;; frq mod osc1
    (set_lfo inst PARAM_LFO1 1.0 3.128)    
    (set_mod_matrix inst PARAM_LFO1 PARAM_OSC1_FRQ 7.0)
    
    ;; amp env
    (set_amp_env inst 50.0 0.0 1.0 3000.0)
    
    ;; filter
    (set_filter_frq inst 800.0) ;; 700 hz
    (set_filter_env_amt inst 0.0) ;; no envelope
    (set_filter_res inst 0.2)
    (set_filter_env_follow_frq inst 2.0)
    
    ;; set reverb
    (set_reverb_mix inst 0.65)
    (set_reverb_size inst 0.8)
    
    ;; mono synth
    (set_monophonic inst)
    (set_portamento inst 60.0)
    (set_legato inst 1)
    
    ;; set gain
    (set_gain inst 6.0)
    void))


(drwho_bass bass)
(drwho_lead lead)

(*metro* 'set-tempo 135.0)

(define R repeat)

(define test
  (lambda (beat dur)
    (playp 8 0 bass 0 `(,@(R `(e3 __ e3) 6) (g3 __ g3) (g3 __ d3))  (cosr 70 20 2) 0.5)
    (playp 32 0 lead 0 `((b4 b4 b4 d5)
                           (b4 b4 b4 __)
                           (b4 g4 g4 b3 d4 (d4 __ c4) b3 (b3 __ a3))
                           (b3 b3 b3 (b3 c4 d4) b3 __ b3 c5))
           (cosr 80 10 3) 2.5)
    (callback (*metro* (+ beat (* .5 dur))) 'test (+ beat dur) dur)))

(test (*metro* 'get-beat 4) 1/4)

